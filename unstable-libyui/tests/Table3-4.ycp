// merge of Table3 and Table4 tests
{
list itemlist1 =
	[ `item(`id(3), "Spaghetti",	  8),
	  `item(`id(4), "Steak Sandwich",  12),
	  `item(`id(1), "Chili",           6),
	  `item(`id(2), "Salami Baguette", nil)
	];

list itemlist2 =
	[ `item(`id(0), "Mercedes",	60000),
	  `item(`id(1), "AUDI",		50000),
	  `item(`id(2), "VW",		40000),
	  `item(`id(3), "BMW",		60000),
	  `item(`id(3), "Porsche",	80000)
	];

list itemslists = [ itemlist1, itemlist2 ];

integer listnum = 0;

UI::OpenDialog (
	`VBox (
		`Heading ("Prices"),
		`MinSize (30, 10,
			`Table (`id(`table), `header("Name", "price"), itemlist1)
			),
		`Right (
			`HBox (
				`PushButton (`id (`lookup), "&Lookup"),
				`PushButton (`id (`next),   "Change &Table Contents"),
				`PushButton (`id (`cancel), "&Close")
				)
			)
		)
	);

any ret = nil;
while (ret != `cancel) {
	ret = UI::UserInput();

	if (ret == `next) {
		listnum = 1 - listnum;
		UI::ChangeWidget(`id(`table), `Items, select(itemslists, listnum, nil));
	}

	if (ret == `lookup) {
		any id = UI::QueryWidget(`id(`table), `CurrentItem);
		if (is(id, integer)) {
			string text = sformat (
				"Line: %1", UI::QueryWidget(`id(`table), `Item(id)));
			UI::OpenDialog (
				`MarginBox (1, 0.2,
					`VBox (
						`Left (`Label ("Current Table Item")),
						`Label(`opt(`outputField), text),
						`PushButton("&OK")
						)
					)
				);
		UI::UserInput();
		UI::CloseDialog();
		}
	}
}

UI::CloseDialog();
}

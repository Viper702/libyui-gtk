// RichText test (with a list of examples)
{
	list texts = [
		// simple
		"<p><a href=\"nowhere\">Hello world!</a></p>",
		"<h1>Operating Systems</h1><h2>Linux</h2> Suse",

		// the br & hr tags
		"Drinking <u>ice</u> tea <br> <hr> is <b>good</b> for you.",

		// unclosed tags
		"No outer tag<p>Unclosed paragraphs<p>several of them <b>unclosed bold</b>",

		// Some more complete text, with an image
		"<center><h1><font color=blue>Super <font color=red>Man</font></font> "
		+ "to the rescue</h1></center><a "
		+ "href=http://en.wikipedia.org/wiki/Superman>Super Man</a> is a very "
		+ "estimated super hero in Metropolis, but he is looked down by others "
		+ "like mad scientists and politiceans. <img src=image.gif>",

		// table (from SlideShow)
		"<table cellspacing=\"10\" cellpadding=\"5\">"
		+ "<tr>"
		+ "<td width =\"*\">"
		+ "<img src=\"/opt/gnome/share/icons/hicolor/32x32/apps/gnome-media-player.png\""
		+ "width=\"100\" align=\"left\">"
		+ "</td>"
		+ "<td width=\"*\">"
		+ "<p><font color=\"#00007f\"><b>XMMS and JuK - Powerful Jukeboxes</b></font></p>"
		+ "<p>XMMS is an excellent sound player for Linux. It is easy to use and supports"
		+ "various formats, including audio CDs. Test the many visualization plug-ins or"
		+ "download your favorite XMMS skins from the web.</p>"
		+ "<p>New in KDE: JuK, which classifies your MP3s and organizes your music"
		+ "collection.</p>"
		+ "<p><b>Want More?</b></p>"
		+ "<p>The SUSE distribution features a wide range of applications for playing"
		+ "your CDs and MP3 songs. For example, KsCD is a user-friendly CD player. The"
		+ "track information for most CDs is available on the Internet. Simply activate"
		+ "the respective function to display the list.</p>"
		+ "</td>"
		+ "</tr>"
		+ "</table>"
	];

	UI::OpenDialog (
		`VBox (
			`RichText (`id (`rtext), texts[0]:nil),
			`MultiLineEdit (`id (`source), `opt(`notify), "Source code", texts[0]:nil),
			`Label (`id(`link), `opt(`hstretch), ""),
			`Slider (`id (`slider), `opt(`notify), "Test text case:", 0, size (texts)-1, 0),
			`PushButton (`id (`close), `opt(`icon ("close")), "&Close")
		)
	);

	any ret = nil;
	repeat
	{
		ret = UI::UserInput();

		if (ret == `slider)
		{
			integer cur_text = (integer) UI::QueryWidget(`id(`slider), `Value );
			UI::ChangeWidget (`id (`source), `Value, texts[cur_text]:nil);
			UI::ChangeWidget (`id (`rtext),  `Value, texts[cur_text]:nil);
			UI::ChangeWidget (`id(`link), `Value, "");
		}

		else if (ret == `source)
		{
			UI::ChangeWidget (`id (`rtext), `Value,
			                  (string) UI::QueryWidget(`id(`source), `Value));
		}

		else
		{  // RichText doesn't seem to pass its id...
			string url = sformat ("%1", ret);
			UI::ChangeWidget (`id(`link), `Value, "You pressed: " + url);
		}

	} until (ret == `close || ret == `cancel);

	UI::CloseDialog();
}
